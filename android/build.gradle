apply plugin: 'maven'
apply plugin: 'com.android.library'

import org.gradle.internal.os.OperatingSystem;

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    google()
    maven {
     // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url "$rootDir/../node_modules/react-native/android"
    }
}

android {
    ndkVersion '19.2.5345600'

    compileSdkVersion 28

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    // To let the libs be included in other projects
    externalNativeBuild {
        ndkBuild {
            path "jni/Android.mk"
        }
    }

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode project.VERSION_CODE.toInteger()
        versionName project.VERSION_NAME
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
}

dependencies {
    // TODO are these required in this lib project?
    //compile fileTree(include: ['*.jar'], dir: 'libs')
   //compile 'com.android.support:appcompat-v7:23.1.1'
   testImplementation group: 'com.google.guava', name: 'guava', version: '23.0'
   testImplementation 'junit:junit:4.12'
   androidTestImplementation group: 'com.google.guava', name: 'guava', version: '23.0'
   androidTestImplementation 'junit:junit:4.12'
   androidTestImplementation 'com.android.support.test:runner:0.5'
   implementation 'com.facebook.react:react-native:+'
}

buildscript {
    System.properties['com.android.build.gradle.overrideVersionCheck'] = 'true'
    repositories {
      mavenCentral()
    	jcenter()
      //https://developer.android.com/topic/libraries/testing-support-library/packages.html#gradle-maven-dependencies
      maven {
          url "https://maven.google.com"
      }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
    }
}

apply plugin: "c"

model {
  flavors {
    host // Also compile the native library for the host OS to allow running tests which access the library on the host OS.
  }
  repositories {
    libs(PrebuiltLibraries) {
      jdk {
        headers.srcDir "${System.getenv("JAVA_HOME")}/include"
        headers.srcDir "${System.getenv("JAVA_HOME")}/include/linux" // this seems to be host OS dependent, but should be host OS independent
        headers.srcDir "${System.getenv("JAVA_HOME")}/include/darwin"
      }
      sodium {
        headers.srcDir "${projectDir}/libsodium/libsodium-host/include"
        binaries.withType(StaticLibraryBinary) { binary ->
          staticLibraryFile = file("${projectDir}/libsodium/libsodium-host/lib/libsodium.a")
        }       
      }
    }
  }
  components {
    sodiumjni(NativeLibrarySpec) {
      sources {
        c {
          source {
            srcDir "${projectDir}/jni"
            include "sodium_wrap.c"
            include "dummy.c"
          }
          lib library: 'jdk',    linkage: 'api'
          lib library: 'sodium', linkage: 'static'
        }
        binaries.all {
            cCompiler.args "-I${System.getenv("ANDROID_NDK_HOME")}/sysroot/usr/include"
            if (OperatingSystem.current().isLinux()) {
              //flag not on macos clang
              linker.args "-Wl,-Bsymbolic" // to work around error "shared library text segment is not shareable"
          }
        }
      }
    }
  }
}

tasks.withType(Test) { task ->
  systemProperty "java.library.path", "${projectDir}/build/libs/sodiumjni/shared"
  task.dependsOn('sodiumjniSharedLibrary')
}

// task('generateSWIGsource') {
//   inputs.dir   fileTree(dir: "${projectDir}/jni")
//   outputs.dir("${projectDir}/src/main/java/org/libsodium/jni")
//   outputs.file "${projectDir}/jni/sodium_wrap.c"
//   doFirst {
//     exec {
//       workingDir "${projectDir}/jni"
//       commandLine 'swig','-java','-package','com.reactnativelibsodium.jni','-outdir','../src/main/java/com/reactnativelibsodium/jni','sodium.i'
//     }
//   }
// }

gradle.projectsEvaluated {
  generateJsonModelDebug.dependsOn   'generateSWIGsource'
  generateJsonModelRelease.dependsOn 'generateSWIGsource'
}
